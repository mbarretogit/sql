/* --------------------------------------------
              TABELA DE LOG
-----------------------------------------------*/

IF EXISTS(SELECT TOP 1 1 FROM SYSOBJECTS WHERE NAME = 'LY_CUSTOM_LOG_GERA_COBRANCA_ALUNO' AND TYPE = 'U')
DROP TABLE LY_CUSTOM_LOG_GERA_COBRANCA_ALUNO
GO

CREATE TABLE LY_CUSTOM_LOG_GERA_COBRANCA_ALUNO
(
  ID INT IDENTITY (1,1)
, FACULDADE VARCHAR(20)
, ALUNO VARCHAR(20)
, RESP VARCHAR(20)
, ANO INT
, MES INT
, COBRANCA INT
, DATA_DE_VENCIMENTO DATETIME
, DATA_DE_GERACAO DATETIME
)
GO

/* --------------------------------------------
     PROCEDURE PARA JOB DE FATURAMENTO
-----------------------------------------------*/

IF EXISTS(SELECT TOP 1 1 FROM SYSOBJECTS WHERE NAME = 'PROC_GERA_COBRANCA_ALUNO_JOB' AND TYPE = 'P')
DROP PROCEDURE PROC_GERA_COBRANCA_ALUNO_JOB
GO

CREATE PROCEDURE PROC_GERA_COBRANCA_ALUNO_JOB
(
  @P_ANO INT
, @P_PERIODO INT
, @P_FACULDADE VARCHAR(20) = NULL
, @P_CURSO VARCHAR(20) = NULL
, @P_ALUNO VARCHAR(20) = NULL
)
AS
BEGIN

  SET NOCOUNT ON;

  -- TRATAMENTO DA SESSÃO
  DELETE LY_USUARIO_SESSAO WHERE SESSAO_ID = @@SPID
  
  INSERT INTO LY_USUARIO_SESSAO(USUARIO, SESSAO_ID, PRIVIL, RESTRICAO_SIMNAO, RESTRICAO_UNIDADE, RESTRICAO_CURSO, RESTRICAO_UNID_FIS)
  SELECT 'zeus', @@SPID, 'S' PRIVIL, 'S' RESTRICAO_SIMNAO, 'S' RESTRICAO_UNIDADE, 'N' RESTRICAO_CURSO, 'S' RESTRICAO_UNID_FIS 

  -- VARIÁVEIS
  DECLARE @V_ANO_INICIAL INT
  DECLARE @V_MES_INICIAL INT
  DECLARE @V_ANO INT
  DECLARE @V_MES INT
  DECLARE @V_MES_CHAR VARCHAR(2)
  DECLARE @V_FACULDADE VARCHAR(20)
  DECLARE @V_CURSO VARCHAR(20)
  DECLARE @V_ALUNO VARCHAR(20)
  DECLARE @V_RESP VARCHAR(20)
  DECLARE @V_TEM_COBRANCA_ALUNO INT
  DECLARE @V_DATA_VENC DATETIME
  DECLARE @V_NUM_PARCELAS INT
  DECLARE @i INT
  DECLARE @V_SESSAO_ID_STR VARCHAR(40) 
  DECLARE @V_DT_FINAL_REST DATETIME

  DECLARE C_CURSOR CURSOR FAST_FORWARD FOR

  SELECT DISTINCT 
	C.FACULDADE
  , C.CURSO
  , A.ALUNO
  , P.RESP
  , P.ANO_INICIAL
  , P.MES_INICIAL
  , P.NUM_PARCELAS
  FROM LY_ALUNO A
  JOIN LY_CURSO C
    ON (C.CURSO = A.CURSO)
  JOIN VW_MATRICULA_E_PRE_MATRICULA M
    ON M.ALUNO = A.ALUNO
  JOIN LY_PLANO_PGTO_PERIODO P
    ON (P.ALUNO = A.ALUNO AND P.ANO = M.ANO AND P.PERIODO = M.SEMESTRE)
  WHERE (M.SIT_MATRICULA = 'Matriculado') -- OR M.SIT_MATRICULA = 'Pré-Matriculado')
  AND M.ANO = @P_ANO
  AND M.SEMESTRE = @P_PERIODO
  AND ((C.FACULDADE = @P_FACULDADE AND @P_FACULDADE IS NOT NULL) OR @P_FACULDADE IS NULL)
  AND ((C.CURSO = @P_CURSO AND @P_CURSO IS NOT NULL) OR @P_CURSO IS NULL)
  AND ((A.ALUNO = @P_ALUNO AND @P_ALUNO IS NOT NULL) OR @P_ALUNO IS NULL)
  ORDER BY C.FACULDADE, C.CURSO, A.ALUNO

  OPEN C_CURSOR
  FETCH NEXT FROM C_CURSOR INTO @V_FACULDADE, @V_CURSO, @V_ALUNO, @V_RESP
							  , @V_ANO_INICIAL, @V_MES_INICIAL, @V_NUM_PARCELAS

  WHILE(@@FETCH_STATUS = 0)
  BEGIN
	SET @i = 1
	SET @V_ANO = @V_ANO_INICIAL
	SET @V_MES = @V_MES_INICIAL
	PRINT 'Aluno....: ' + @V_ALUNO

	WHILE
	(
		@i <= @V_NUM_PARCELAS
	)
	BEGIN
		SET @V_TEM_COBRANCA_ALUNO = 0 

		SELECT @V_TEM_COBRANCA_ALUNO = COUNT(*)
		FROM LY_COBRANCA C
		WHERE 1 =1
		--AND ANO = @V_ANO
		--AND MES = @V_MES
		AND ALUNO = @V_ALUNO
		AND RESP = @V_RESP
		AND NUM_COBRANCA = 1
		AND ESTORNO = 'N'
		AND EXISTS
		(
			SELECT TOP 1 1
			FROM LY_ITEM_LANC IL
			JOIN LY_LANC_DEBITO D
				ON IL.LANC_DEB = D.LANC_DEB
			WHERE D.ANO_REF = @P_ANO
			AND D.PERIODO_REF = @P_PERIODO
			AND C.COBRANCA = IL.COBRANCA
			AND IL.ANO_REF_BOLSA = @V_ANO
			AND IL.MES_REF_BOLSA = @V_MES
			AND IL.MOTIVO_DESCONTO IS NULL
			AND IL.NUM_BOLSA IS NULL
			AND IL.ACORDO IS NULL
		)

		IF @V_TEM_COBRANCA_ALUNO = 0
		BEGIN
			DELETE zzCRO_Erros WHERE spid = @@SPID

			IF @V_MES < 10 
				SET @V_MES_CHAR = '0' + CONVERT(VARCHAR,@V_MES)
			ELSE
				SET @V_MES_CHAR = CONVERT(VARCHAR,@V_MES)

			SET @V_DATA_VENC = CONVERT(DATETIME,CONVERT(VARCHAR,@P_ANO) + '-' + @V_MES_CHAR + '-05')

			PRINT '     Ano/Mês: ' + CONVERT(VARCHAR,@V_ANO) + '/' + @V_MES_CHAR

			EXEC GERA_PROXIMA_COBRANCA
			  @P_ANO		-- @p_ano
			, @P_PERIODO	-- @p_periodo
			, NULL			-- @p_grupo_desconto
			, 'GE01'		-- @p_grupo_encargo
			, @V_ALUNO		-- @p_Aluno
			, @V_RESP		-- @p_Resp
			, @V_DATA_VENC	-- @p_DtVenc
			, 'S'			-- @p_utiliza_vencimento_pref
			, 'N'			-- @p_gera_proxima_cobranca
			, @V_ANO		-- @p_ano_ini
			, @V_MES		-- @p_mes_ini
			, @V_ANO		-- @p_ano_fim
			, @V_MES		-- @p_mes_fim
			, '1'			-- @p_num_cobranca
			, 'N'			-- @p_proxima_serie
			, 'N'			-- @p_nverif_valor_a_lanc
			, 'N'			-- @p_gera_arrasto
			, NULL			-- @p_dtbasearrasto
			, NULL			-- @p_dtlimitearrasto
			, NULL			-- @p_tipo_arrasto
			, NULL			-- @p_Cod_Lanc_Arrasto
			, NULL			-- @p_Vr_Limite_Arrasto

			SELECT 
				@V_TEM_COBRANCA_ALUNO = COBRANCA
			,	@V_DATA_VENC = DATA_DE_VENCIMENTO
			FROM LY_COBRANCA
			WHERE ANO = @V_ANO
			AND MES = @V_MES
			AND ALUNO = @V_ALUNO
			AND RESP = @V_RESP
			AND NUM_COBRANCA = 1
			AND ESTORNO = 'N'
			AND DATA_DE_GERACAO = DBO.FN_DATADIASEMHORA(GETDATE())
			AND DATA_DE_FATURAMENTO IS NULL

			IF @V_TEM_COBRANCA_ALUNO > 0
			BEGIN
				UPDATE LY_COBRANCA 
				SET LOTE = CONVERT(INT,CONVERT(VARCHAR,@V_ANO) + @V_MES_CHAR) 
				WHERE COBRANCA = @V_TEM_COBRANCA_ALUNO

				SET @V_DT_FINAL_REST = @V_DATA_VENC - 1

				IF @P_FACULDADE IS NULL
					SELECT @P_FACULDADE = C.FACULDADE
					FROM LY_CURSO C
					JOIN LY_COBRANCA COB
						ON (C.CURSO = COB.CURSO)
					WHERE COB.COBRANCA = @V_TEM_COBRANCA_ALUNO

				-- GERAÇÃO DA RESTITUIÇÃO
				EXEC PROC_GERA_RESTITUICOES
				  @P_FACULDADE		-- @p_unidade,
				, NULL				-- @p_curso
				, '2018-01-01'		-- @p_dt_inicial_cobr
				, @V_DT_FINAL_REST	-- @p_dt_final_cobr
				, @V_DATA_VENC		-- @p_dt_inicial_rest
				, @V_DATA_VENC		-- @p_dt_final_rest
				, @V_RESP			-- @p_resp_finan

				-- GERAÇÃO DO FATURAMENTO
				SELECT @V_SESSAO_ID_STR = RTRIM(CONVERT(VARCHAR(40),@@SPID))    

				DELETE LY_ITENS_AUX
				WHERE SESSAO_ID = @V_SESSAO_ID_STR    
     
				INSERT INTO LY_ITENS_AUX (SESSAO_ID, CHAVE, COBRANCA)    
				SELECT @V_SESSAO_ID_STR, COBRANCA, COBRANCA    
				FROM LY_COBRANCA C
				WHERE C.COBRANCA = @V_TEM_COBRANCA_ALUNO   	     

				EXEC GERA_BOLETO_RESP
				  @V_SESSAO_ID_STR
				, NULL			-- @p_Banco
				, NULL			-- @p_Agencia
				, NULL			-- @p_Conta
				, NULL			-- @p_Convenio
				, NULL			-- @p_Carteira
				, @V_RESP		-- @p_Resp
				, 'S'			-- @p_ApenasFaturar
				, @V_DATA_VENC	-- @p_DtVencIni      
				, 'N'			-- @p_Usa_Cobr_Pre_Selecionadas
				, NULL			-- @p_Lote
				, 'N'			-- @p_Boleto_Zerado
				, 'N'			-- @p_Boleto_Negativo
				, 'N'			-- @p_cobranca_com_nota
				, NULL			-- @p_Apartir_Valor
				, '1'			-- @p_tipo_cobranca
				, 'S'			-- @p_online

				INSERT INTO LY_CUSTOM_LOG_GERA_COBRANCA_ALUNO(FACULDADE, ALUNO, RESP, ANO, MES, COBRANCA, DATA_DE_VENCIMENTO, DATA_DE_GERACAO)
				SELECT @V_FACULDADE, ALUNO, RESP, ANO, MES, COBRANCA, DATA_DE_VENCIMENTO, DATA_DE_GERACAO
				FROM LY_COBRANCA
				WHERE COBRANCA = @V_TEM_COBRANCA_ALUNO
			END
		END

		IF @V_MES <= 11
			SET @V_MES = @V_MES + 1
		ELSE
		BEGIN
			SET @V_ANO = @V_ANO + 1
			SET @V_MES = 1
		END

		SET @i = @i + 1

	END

	FETCH NEXT FROM C_CURSOR INTO @V_FACULDADE, @V_CURSO, @V_ALUNO, @V_RESP
								, @V_ANO_INICIAL, @V_MES_INICIAL, @V_NUM_PARCELAS

  END
  CLOSE C_CURSOR
  DEALLOCATE C_CURSOR

  SET NOCOUNT OFF;

END
GO
